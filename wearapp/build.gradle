apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "com.thewizrd.simpleweather"
        testApplicationId "com.thewizrd.simpleweather.test"
        // Specifies the fully-qualified class name of the test instrumentation runner.
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion 26
        targetSdkVersion 31
        // NOTE: Version Code Format (TargetSDK, Version Name, Build Number, Variant Code (Android: 00, WearOS: 01)
        versionCode 315310301
        versionName "5.3.1"

        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [allowBackup: false]
        }
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [allowBackup: true]
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    bundle {
        language {
            // Specifies that the app bundle should not support
            // configuration APKs for language resources. These
            // resources are instead packaged with each base and
            // dynamic feature APK.
            enableSplit false
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    // Workaround for binding class errors
    sourceSets {
        debug {
            java.srcDirs += 'build/generated/data_binding_base_class_source_out/debug/out'
        }
    }
    flavorDimensions 'gms'
    productFlavors {
        nongms {
            dimension 'gms'
            buildConfigField "boolean", "IS_NONGMS", "true"
        }
        fullgms {
            dimension 'gms'
            buildConfigField "boolean", "IS_NONGMS", "false"
        }
    }
}

dependencies {
    implementation project(":shared_resources")
    implementation project(":unofficialtileapi")
    implementation project(":extras")

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.2'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'

    implementation 'androidx.arch.core:core-runtime:2.1.0'
    implementation 'androidx.room:room-runtime:2.4.0'
    kapt 'androidx.room:room-compiler:2.4.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    implementation 'com.google.android.material:material:1.4.0'

    implementation 'com.google.android.gms:play-services-base:18.0.1'
    implementation 'com.google.android.gms:play-services-location:19.0.0'

    // WearOS
    implementation 'com.google.android.gms:play-services-wearable:17.1.0'
    compileOnly 'com.google.android.wearable:wearable:2.8.1'

    implementation 'androidx.wear:wear:1.2.0'
    implementation 'androidx.wear:wear-phone-interactions:1.0.1'
    implementation 'androidx.wear:wear-remote-interactions:1.0.0'
    implementation 'androidx.wear.watchface:watchface-complications-data:1.0.0'
    implementation 'androidx.wear.watchface:watchface-complications-data-source-ktx:1.0.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-perf'

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.ibm.icu:icu4j:70.1'

    implementation("com.vimeo.stag:stag-library:2.6.0") {
        exclude group: "org.jetbrains", module: "annotations-java5"
    }
    kapt("com.vimeo.stag:stag-library-compiler:2.6.0") {
        exclude group: "org.jetbrains", module: "annotations-java5"
    }
}