plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true",
                        "room.expandProjection": "true"
                ]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        androidTest {
            // Adds exported schema location as test app assets.
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    flavorDimensions 'gms'
    productFlavors {
        nongms {
            dimension 'gms'
            buildConfigField "boolean", "IS_NONGMS", "true"
        }
        fullgms {
            dimension 'gms'
            buildConfigField "boolean", "IS_NONGMS", "false"
        }
    }
    namespace 'com.thewizrd.shared_resources'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'

    // Unit Testing
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_version"

    implementation "androidx.appcompat:appcompat:$appcompat_version"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    // Room migration testing
    androidTestImplementation "androidx.room:room-testing:$room_version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.ibm.icu:icu4j:71.1'

    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    fullgmsImplementation 'com.google.android.gms:play-services-base:18.1.0'
    fullgmsImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlinx_version"

    // Firebase
    fullgmsImplementation platform("com.google.firebase:firebase-bom:$firebase_version")
    fullgmsImplementation 'com.google.firebase:firebase-analytics'
    fullgmsImplementation 'com.google.firebase:firebase-crashlytics'
    fullgmsImplementation 'com.google.firebase:firebase-auth'
    fullgmsImplementation 'com.google.firebase:firebase-firestore'
    fullgmsImplementation 'com.google.firebase:firebase-storage'
    fullgmsImplementation 'com.google.firebase:firebase-database'
    fullgmsImplementation 'com.google.firebase:firebase-config'
}